/-
Copyright (c) 2022 Kevin H. Wilson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin H. Wilson
-/
import analysis.calculus.fderiv
import analysis.calculus.mean_value
import analysis.convex.topology
import analysis.normed_space.is_R_or_C
import data.complex.is_R_or_C
import order.filter.basic

/-!
# Swapping limits and derivatives via uniform convergence

The purpose of this file is to prove that the derivative of the pointwise limit of a sequence of
functions is the pointwise limit of the functions' derivatives when the derivatives converge
_uniformly_. The formal statement appears as `has_fderiv_at_of_tendsto_uniformly_on`.

## Main statements

* `has_fderiv_at_of_tendsto_uniformly_on` : If `f : ‚Ñï ‚Üí E ‚Üí G` is a sequence of functions with
  derivatives `f' : ‚Ñï ‚Üí (E ‚Üí (E ‚ÜíL[ùïú] G))` and the `f` converge pointwise to `g` and the `f'`
  converge _uniformly_ on some closed ball, then the derivative of `g'` is the pointwise limit
  of the `f'` on the closed ball

## Implementation notes

Our proof utilizes three major components:
  * `convex.norm_image_sub_le_of_norm_has_fderiv_within_le`: The mean value inequality for
    vector-valued functions over `‚Ñù` and `‚ÑÇ`
  * `norm_add_le`: The triangle inequality
  * `uniform_cauchy_seq_on.tendsto_uniformly_on_of_tendsto` which allows us to upgrade pointwise
    convergence to uniform convergence by showing that the Cauchy sequences converge uniformly to 0

## Tags

uniform convergence, limits of derivatives
-/

open finset filter metric
open_locale uniformity filter

section convenience_lemmas

/-- Convenience lemma -/
private lemma mul_cancel_inv_left‚ÇÄ {a b : ‚Ñù} (ha : a ‚â† 0) : a‚Åª¬π * (a * b) = b :=
 begin
  conv { congr, congr, skip, rw ‚Üêinv_inv a, },
  have : a‚Åª¬π ‚â† 0, simp [ha],
  rw mul_inv_cancel_left‚ÇÄ this,
end

/-- We'll need to apply the triangle ineqaulity twice, which is easier to do in lemma
format -/
private lemma norm_add_three_le {E : Type*} [semi_normed_group E] {a b c : E} :
  ‚à•a + b + c‚à• ‚â§ ‚à•a‚à• + ‚à•b‚à• + ‚à•c‚à• :=
begin
  refine le_trans (norm_add_le _ _) _,
  exact add_le_add (norm_add_le _ _) rfl.le,
end

/-- Convenience lemma for three way comparisons -/
private lemma lt_of_lt_of_lt_of_le {a b c a' b' c': ‚Ñù} :
  a < a' ‚Üí b < b' ‚Üí c ‚â§ c' ‚Üí a + b + c < a' + b' + c' :=
begin
  intros ha hb hc,
  linarith [ha, hb, hc],
end

end convenience_lemmas

section limits_of_derivatives

variables {E : Type*} [normed_group E] {ùïú : Type*} {G : Type*}
  [is_R_or_C ùïú] [normed_group G] [normed_space ùïú G]
  {f : ‚Ñï ‚Üí E ‚Üí G} {g : E ‚Üí G} {x y z : E} {r R C : ‚Ñù}

/-- For fixed y and z, if `f_n ‚Üí g` pointwise, then the difference quotients
`‚à•z - y‚à•‚Åª¬π ‚Ä¢ (f_n z - f_n y)` converge to the difference quotients
`‚à•z - y‚à•‚Åª¬π ‚Ä¢ (g z - g y)` -/
lemma difference_quotients_converge
  (hfg : ‚àÄ (y : E), y ‚àà closed_ball x r ‚Üí tendsto (Œª n, f n y) at_top (nhds (g y))) :
  ‚àÄ y : E, y ‚àà closed_ball x r ‚Üí ‚àÄ z : E, z ‚àà closed_ball x r ‚Üí
    tendsto (Œª n : ‚Ñï, (‚à•z - y‚à•‚Åª¬π : ùïú) ‚Ä¢ (f n z - f n y))
      at_top (nhds ((‚à•z - y‚à•‚Åª¬π : ùïú) ‚Ä¢ (g z - g y))) :=
(Œª y hy z hz, ((hfg z hz).sub (hfg y hy)).const_smul _)

variables [normed_space ‚Ñù E] [normed_space ùïú E] {f' : ‚Ñï ‚Üí (E ‚Üí (E ‚ÜíL[ùïú] G))} {g' : E ‚Üí (E ‚ÜíL[ùïú] G)}

/-- A convenience theorem for utilizing the mean value theorem for differences of
differentiable functions -/
lemma mean_value_theorem_for_differences {f : E ‚Üí G} {f' : E ‚Üí (E ‚ÜíL[ùïú] G)}
  (hrR : r < R) (hf : ‚àÄ (y : E), y ‚àà ball x R ‚Üí has_fderiv_at f (f' y) y)
  (hg : ‚àÄ (y : E), y ‚àà ball x R ‚Üí has_fderiv_at g (g' y) y)
  (hbound : ‚àÄ (y : E), y ‚àà closed_ball x r ‚Üí ‚à•f' y - g' y‚à• ‚â§ C)
  (hy : y ‚àà closed_ball x r) (hz : z ‚àà closed_ball x r) :
  ‚à•y - z‚à•‚Åª¬π * ‚à•(f y - g y) - (f z - g z)‚à• ‚â§ C :=
begin
  -- Differences of differentiable functions are differentiable and closed balls are
  -- convex, so a bit of annoying symbol pushing will get us the actual theorem

  -- Closed balls are convex
  have hconvex := convex_closed_ball x r,

  -- Differences of differentiable functions are differentiable
  have hderiv : ‚àÄ (y : E), y ‚àà closed_ball x r ‚Üí
    has_fderiv_within_at (f - g) ((f' - g') y) (closed_ball x r) y,
  { intros y hy,
    have : y ‚àà ball x R, {calc dist y x ‚â§ r : hy ... < R : hrR,},
    have := ((hf y this).sub (hg y this)).has_fderiv_within_at,
    simp only [pi.sub_apply],
    have : (Œª x : E, f x - g x) = f - g, { funext, simp only [pi.sub_apply], },
    rwa ‚Üêthis, },

  -- Apply the mean value theorem
  have := convex.norm_image_sub_le_of_norm_has_fderiv_within_le
    hderiv hbound hconvex hz hy,

  -- Auxiliary lemmas necessary for algebraic manipulation
  have h_le : ‚à•y - z‚à•‚Åª¬π ‚â§ ‚à•y - z‚à•‚Åª¬π, { exact le_refl _, },
  have C_nonneg : 0 ‚â§ C,
  { calc 0 ‚â§ ‚à•f' y - g' y‚à• : norm_nonneg _ ... ‚â§ C : hbound y hy, },
  have h_le' : 0 ‚â§ C * ‚à•y - z‚à•, exact mul_nonneg C_nonneg (by simp),

  -- The case y = z is degenerate. Eliminate it
  by_cases h : y = z,
  { simp only [h, C_nonneg, sub_self, norm_zero, mul_zero], },
  have h_ne_zero : ‚à•y - z‚à• ‚â† 0,
  { simp only [ne.def, norm_eq_zero],
    exact Œª hh, h (sub_eq_zero.mp hh), },

  -- Final manipulation
  have := mul_le_mul this h_le (by simp) h_le',
  simp only [pi.sub_apply] at this,
  rw mul_inv_cancel_right‚ÇÄ h_ne_zero at this,
  rwa mul_comm,
end

/-- If `f_n ‚Üí g` pointwise and the derivatives `(f_n)' ‚Üí h` _uniformly_ converge, then
in fact for a fixed `y`, the difference quotients `‚à•z - y‚à•‚Åª¬π ‚Ä¢ (f_n z - f_n y)` converge
_uniformly_ to `‚à•z - y‚à•‚Åª¬π ‚Ä¢ (g z - g y)` -/
lemma difference_quotients_converge_uniformly
  (hrR : r < R)
  (hf : ‚àÄ (n : ‚Ñï), ‚àÄ (y : E), y ‚àà ball x R ‚Üí has_fderiv_at (f n) (f' n y) y)
  (hfg : ‚àÄ (y : E), y ‚àà closed_ball x r ‚Üí tendsto (Œª n, f n y) at_top (nhds (g y)))
  (hfg' : tendsto_uniformly_on f' g' at_top (closed_ball x r)) :
  ‚àÄ y : E, y ‚àà closed_ball x r ‚Üí
    tendsto_uniformly_on
      (Œª n : ‚Ñï, Œª z : E, (‚à•z - y‚à•‚Åª¬π : ùïú) ‚Ä¢ ((f n z) - (f n y)))
      (Œª z : E, (‚à•z - y‚à•‚Åª¬π : ùïú) ‚Ä¢ ((g z) - (g y))) at_top ((closed_ball x r)) :=
begin
  -- Proof strategy: Rewrite the Cauchy sequence of difference quotients as
  -- a difference quotient. Then apply the mean value theorem and the uniform
  -- convergence of the difference of derivatives
  intros y hy,
  refine uniform_cauchy_seq_on.tendsto_uniformly_on_of_tendsto _
    (difference_quotients_converge hfg y hy),
  rw uniform_cauchy_seq_on_iff,
  intros Œµ hŒµ,
  have := hfg'.uniform_cauchy_seq_on,
  rw metric.uniform_cauchy_seq_on_iff at this,
  have half_eps_ge_zero : 2‚Åª¬π * Œµ > 0, { simp [hŒµ.lt], },
  have half_eps_lt_eps : 2‚Åª¬π * Œµ < Œµ,
  { -- This seems like it should be golfable?
    have := half_lt_self hŒµ.lt,
    ring_nf at this,
    ring_nf,
    exact this, },
  rcases (this (2‚Åª¬π * Œµ) half_eps_ge_zero) with ‚ü®N, hN‚ü©,
  use N,
  intros m hm n hn z hz,
  specialize hN m hm n hn,
  have : ‚àÄ (x_1 : E), x_1 ‚àà closed_ball x r ‚Üí ‚à•f' m x_1 - f' n x_1‚à• ‚â§ 2‚Åª¬π * Œµ,
  { intros y hy,
    rw ‚Üêdist_eq_norm,
    exact (hN y hy).le, },
  have mvt := mean_value_theorem_for_differences hrR (hf m) (hf n) this hz hy,

  rw [dist_eq_norm, ‚Üêsmul_sub, norm_smul, norm_inv, is_R_or_C.norm_coe_norm],
  -- This would work with `ring` but this is no longer a `ring`. Is there a
  -- `comm_group` equivalent of `ring`?
  have : f m z - f m y - (f n z - f n y) = f m z - f n z - (f m y - f n y),
  { rw [‚Üêsub_add, ‚Üêsub_add, sub_sub, sub_sub],
    conv { congr, congr, congr, skip, rw add_comm, }, },
  rw this,
  exact lt_of_le_of_lt mvt half_eps_lt_eps,
end

lemma uniform_convergence_of_uniform_convergence_derivatives
  (hrpos : 0 < r)
  (hrR : r < R)
  (hf : ‚àÄ (n : ‚Ñï), ‚àÄ (y : E), y ‚àà ball x R ‚Üí has_fderiv_at (f n) (f' n y) y)
  (hfg : ‚àÄ (y : E), y ‚àà closed_ball x r ‚Üí tendsto (Œª n, f n y) at_top (nhds (g y)))
  (hfg' : tendsto_uniformly_on f' g' at_top (closed_ball x r)) :
  tendsto_uniformly_on f g at_top (closed_ball x r) :=
begin
  -- Proof strategy: We have assumed that f ‚Üí g pointwise, so it suffices to show that
  -- `f` is a *uniform* cauchy sequence on `closed_ball x r`. But for any `y`, we have
  -- `|f m y - f n y| ‚â§ |(f m - f n) y - (f m - f n) x| + |f m x - f n x|` by
  -- the triangle inequality and "adding zero". Importantly, note that `x` is fixed.
  --
  -- The first of these summands can be bounded using the fact that the difference
  -- quotients converge uniformly. The latter follows from the fact that `Œª n, f n x` is
  -- a (not-necessarily uniform) cauchy sequence.
  refine uniform_cauchy_seq_on.tendsto_uniformly_on_of_tendsto _ hfg,
  rw metric.uniform_cauchy_seq_on_iff,
  intros Œµ hŒµ,

  -- Get the bound for |f m x - f n x|
  have := metric.cauchy_seq_iff.mp (hfg x (by simp [hrpos.le])).cauchy_seq,
  have two_inv_pos : 0 < (2 : ‚Ñù)‚Åª¬π, simp,
  have Œµ_over_two_pos : 0 < (2‚Åª¬π * Œµ),
  { exact mul_pos two_inv_pos hŒµ.lt, },
  cases this (2‚Åª¬π * Œµ) Œµ_over_two_pos.gt with N1 hN1,

  -- The mean value theorem will let us |(f m - f n) y - (f m - f n) x| up to a factor
  -- of diam closed_ball x r = 2 * r. Choose N2 with this in mind
  have foo := metric.uniform_cauchy_seq_on_iff.mp hfg'.uniform_cauchy_seq_on,
  have : 0 < (2‚Åª¬π * r‚Åª¬π * Œµ),
  { exact mul_pos (mul_pos (by norm_num) (by simp [hrpos])) hŒµ.lt, },
  specialize foo (2‚Åª¬π * r‚Åª¬π * Œµ) this.gt,
  cases foo with N2 hN2,

  -- Some annoying manipulation
  let N := max N1 N2,
  refine ‚ü®N, Œª m hm n hn y hy, _‚ü©,
  rw dist_eq_norm,

  -- Apply the triangle inequality
  have : f m y - f n y = (f m y - f n y) - (f m x - f n x) + (f m x - f n x),
  { rw sub_add_cancel, },
  rw this,
  have : ‚à•f m y - f n y - (f m x - f n x) + (f m x - f n x)‚à• ‚â§
    ‚à•f m y - f n y - (f m x - f n x)‚à• + ‚à•f m x - f n x‚à•,
  { exact norm_add_le _ _, },
  refine lt_of_le_of_lt this _,

  -- The case y = x is trivial and causes some divide by zero errors throughout the
  -- proof, so we just take care of it now
  by_cases hyxx : y = x,
  { simp [hyxx],
    rw ‚Üêdist_eq_norm,
    have := hN1 m
      (le_trans (le_max_left N1 N2) hm.le) n (le_trans (le_max_left N1 N2) hn.le),
    transitivity,
    exact this,
    rw mul_lt_iff_lt_one_left hŒµ.lt,
    norm_num, },

  -- Conveniences that the ring solver can't figure out on its own
  have hxyy : y - x ‚â† 0, exact Œª H, hyxx (sub_eq_zero.mp H),
  have hxyy' : ‚à•y - x‚à• ‚â† 0, simp [hxyy],

  -- Multiply and divide by the difference quotient denominator
  have : ‚à•f m y - f n y - (f m x - f n x)‚à• = ‚à•y - x‚à• * (‚à•y - x‚à•‚Åª¬π * ‚à•f m y - f n y - (f m x - f n x)‚à•),
  { exact (mul_inv_cancel_left‚ÇÄ hxyy' _).symm, },
  rw this,

  specialize hN2 m (ge_trans hm (by simp)) n (ge_trans hn (by simp)),
  have : ‚àÄ (x_1 : E), x_1 ‚àà closed_ball x r ‚Üí ‚à• f' m x_1 - f' n x_1‚à• ‚â§ 2‚Åª¬π * r‚Åª¬π * Œµ, {
    intros y hy,
    rw ‚Üêdist_eq_norm,
    exact (hN2 y hy).le,
  },
  have hxb : x ‚àà closed_ball x r, simp [hrpos.le],
  have mvt := mean_value_theorem_for_differences hrR (hf m) (hf n) this hy hxb,
  specialize hN1 m (ge_trans hm (by simp)) n (ge_trans hn (by simp)),
  rw dist_eq_norm at hN1,

  have : Œµ = (2‚Åª¬π * Œµ) + (2‚Åª¬π * Œµ), ring,
  rw this,
  have : r‚Åª¬π * r = 1, { exact inv_mul_cancel hrpos.ne.symm, },

  have : ‚à•y - x‚à• * (‚à•y - x‚à•‚Åª¬π * ‚à•f m y - f n y - (f m x - f n x)‚à•) ‚â§ 2‚Åª¬π * Œµ,
  { have : ‚à•y - x‚à• ‚â§ r, { rw [mem_closed_ball, dist_eq_norm] at hy, exact hy, },
    calc ‚à•y - x‚à• * (‚à•y - x‚à•‚Åª¬π * ‚à•f m y - f n y - (f m x - f n x)‚à•) ‚â§ r * (2‚Åª¬π * r‚Åª¬π * Œµ) :
      mul_le_mul this mvt (mul_nonneg (by simp) (by simp)) (hrpos.le)
    ... = 2‚Åª¬π * Œµ : begin
      ring_nf,
      rw [mul_assoc, inv_mul_cancel hrpos.ne.symm],
      ring,
    end },
  exact add_lt_add_of_le_of_lt this hN1,
end

/-- (d/dx) lim_{n ‚Üí ‚àû} f_n x = lim_{n ‚Üí ‚àû} f'_n x on a closed ball when the f'_n
converge _uniformly_ to their limit. -/
lemma has_fderiv_at_of_tendsto_uniformly_on
  (hrR : r < R)
  (hf : ‚àÄ (n : ‚Ñï), ‚àÄ (y : E), y ‚àà ball x R ‚Üí has_fderiv_at (f n) (f' n y) y)
  (hfg : ‚àÄ (y : E), y ‚àà closed_ball x r ‚Üí tendsto (Œª n, f n y) at_top (nhds (g y)))
  (hfg' : tendsto_uniformly_on f' g' at_top (closed_ball x r)) :
  ‚àÄ y : E, y ‚àà ball x r ‚Üí has_fderiv_at g (g' y) y :=
begin
  -- We do the famous "Œµ / 3 proof" which will involve several bouts of utilizing
  -- uniform continuity. First we setup our goal in terms of Œµ and Œ¥
  intros y hy,
  rw [has_fderiv_at_iff_tendsto, tendsto_nhds_nhds],

  -- Now some important auxiliary facts such as:
  have hrpos : 0 < r,
  { calc 0 ‚â§ dist y x : dist_nonneg ... < r : mem_ball.mp hy, },

  have hyc : y ‚àà closed_ball x r,
  { exact (mem_ball.mp hy).le, },

  -- uniform convergence of the derivatives implies uniform convergence of the primal
  have hfguc := uniform_convergence_of_uniform_convergence_derivatives hrpos hrR hf hfg hfg',

  -- convergence of the primal and uniform convergence of the derivatives implies
  -- uniform convergence of the difference quotients
  have hdiff := difference_quotients_converge_uniformly hrR hf hfg hfg' y hyc,

  -- The first (Œµ / 3) comes from the convergence of the derivatives
  intros Œµ hŒµ,
  have : 0 < (3 : ‚Ñù)‚Åª¬π, simp, linarith,
  have Œµ_over_three_pos : 0 < (3‚Åª¬π * Œµ),
  { exact mul_pos this hŒµ.lt, },

  rw tendsto_uniformly_on_iff at hfg',
  specialize hfg' (3‚Åª¬π * Œµ) Œµ_over_three_pos.gt,
  rw eventually_at_top at hfg',
  rcases hfg' with ‚ü®N1, hN1‚ü©,

  -- The second (Œµ / 3) comes from the uniform convergence of the difference quotients
  rw tendsto_uniformly_on_iff at hdiff,
  specialize hdiff (3‚Åª¬π * Œµ) Œµ_over_three_pos.gt,
  rw eventually_at_top at hdiff,
  rcases hdiff with ‚ü®N2, hN2‚ü©,

  -- These two N determine our final N
  let N := max N1 N2,

  -- The final (Œµ / 3) comes from the definition of a derivative
  specialize hf N y (calc dist y x < r : hy ... < R : hrR),
  rw has_fderiv_at_iff_tendsto at hf,
  rw tendsto_nhds_nhds at hf,
  specialize hf (3‚Åª¬π * Œµ) Œµ_over_three_pos.gt,
  rcases hf with ‚ü®Œ¥', hŒ¥', hf‚ü©,

  -- Choose our final Œ¥
  let Œ¥ := min (r - dist y x) Œ¥',
  have hŒ¥ : Œ¥ > 0,
  { refine lt_min _ hŒ¥'.lt,
    rw sub_pos,
    exact hy, },

  -- Start the final manipulation
  use [Œ¥, hŒ¥],
  intros x' hx',
  have hxc : x' ‚àà closed_ball x r,
  { have foo := calc dist x' y < Œ¥ : hx' ... ‚â§ r - dist y x : by simp [Œ¥],
    calc dist x' x ‚â§ dist x' y + dist y x : dist_triangle _ _ _ ... ‚â§ r : by linarith, },
  have hxy : dist x' y < Œ¥', calc dist x' y < Œ¥ : hx' ... ‚â§ Œ¥' : by simp [Œ¥],
  specialize hf hxy,

  -- There's a technical issue where we need to rule out the case y = x'
  by_cases hy' : y = x',
  { simp [hy', hŒµ.lt], },
  have hx'y : x' - y ‚â† 0, exact Œª H, hy' (sub_eq_zero.mp H).symm,
  have hx'yy : 0 < ‚à•x' - y‚à•, simp only [hx'y, norm_pos_iff, ne.def, not_false_iff],

  -- Now that this case is ruled out, we begin setting up our final outcome:
  -- rewrite Œµ as 3 * (3‚Åª¬π * Œµ)
  have : Œµ = (3‚Åª¬π * Œµ) + (3‚Åª¬π * Œµ) + (3‚Åª¬π * Œµ),
  { ring, },
  rw this,

  -- Our three inequalities come from `hf`, `hN1`, and `hN2`. Get them and the goal in
  -- shape for the final triangle inequality application
  specialize hN1 N (by simp) y hyc,
  rw dist_comm at hN1,
  have hN1 := (f' N y - g' y).le_of_op_norm_le hN1.le (x' - y),
  rw ‚Üêmul_inv_le_iff' hx'yy at hN1,
  rw mul_comm at hN1,

  specialize hN2 N (by simp) x' hxc,
  rw [dist_eq_norm, ‚Üêsmul_sub, norm_smul] at hN2,
  simp only [norm_inv, is_R_or_C.norm_coe_norm] at hN2,

  rw dist_eq_norm at hf,
  simp only [map_sub, sub_zero, norm_mul, norm_inv, norm_norm] at hf,

  rw dist_eq_norm,
  simp only [algebra.id.smul_eq_mul, sub_zero, norm_mul, norm_inv, norm_norm],

  -- Get into shape for triangle inequality
  have : g x' - g y - (g' y) (x' - y) =
    (g x' - g y - (f N x' - f N y)) +
    ((f N x' - f N y) - ((f' N y) x' - (f' N y) y)) +
    ((f' N y - g' y) (x' - y)), simp,
  rw this,

  have : ‚à•x' - y‚à•‚Åª¬π * ‚à•(g x' - g y - (f N x' - f N y)) +
    ((f N x' - f N y) - ((f' N y) x' - (f' N y) y)) +
    ((f' N y - g' y) (x' - y))‚à• ‚â§
    ‚à•x' - y‚à•‚Åª¬π * ‚à•(g x' - g y - (f N x' - f N y))‚à• +
    ‚à•x' - y‚à•‚Åª¬π * ‚à•((f N x' - f N y) - ((f' N y) x' - (f' N y) y))‚à• +
    ‚à•x' - y‚à•‚Åª¬π * ‚à•((f' N y - g' y) (x' - y))‚à•,
  { rw [‚Üêmul_add (‚à•x' - y‚à•‚Åª¬π) _ _, ‚Üêmul_add (‚à•x' - y‚à•‚Åª¬π) _ _],
    have : ‚à•x' - y‚à•‚Åª¬π ‚â§ ‚à•x' - y‚à•‚Åª¬π, exact le_refl _,
    refine mul_le_mul this norm_add_three_le (by simp) (by simp), },

  exact lt_of_le_of_lt this (lt_of_lt_of_lt_of_le hN2 hf hN1),
end

end limits_of_derivatives
