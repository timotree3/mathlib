/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import measure_theory.constructions.borel_space
import measure_theory.function.l1_space
import topology.instances.discrete

/-!
# Filtration and stopping time

This file defines some standard definition from the theory of stochastic processes including
filtrations and stopping times. These definitions are used to model the amount of information
at a specific time and is the first step in formalizing stochastic processes.

## Main definitions

* `measure_theory.filtration`: a filtration on a measurable space
* `measure_theory.adapted`: a sequence of functions `u` is said to be adapted to a
  filtration `f` if at each point in time `i`, `u i` is `f i`-measurable
* `measure_theory.filtration.natural`: the natural filtration with respect to a sequence of
  measurable functions is the smallest filtration to which it is adapted to
* `measure_theory.is_stopping_time`: a stopping time with respect to some filtration `f` is a
  function `Ï„` such that for all `i`, the preimage of `{j | j â‰¤ i}` along `Ï„` is
  `f i`-measurable
* `measure_theory.is_stopping_time.measurable_space`: the Ïƒ-algebra associated with a stopping time

## Tags

filtration, stopping time, stochastic process

-/

open topological_space filter
open_locale classical measure_theory nnreal ennreal topological_space big_operators

namespace measure_theory

/-- A `filtration` on measurable space `Î±` with Ïƒ-algebra `m` is a monotone
sequence of sub-Ïƒ-algebras of `m`. -/
structure filtration {Î± : Type*} (Î¹ : Type*) [preorder Î¹] (m : measurable_space Î±) :=
(seq   : Î¹ â†’ measurable_space Î±)
(mono' : monotone seq)
(le'   : âˆ€ i : Î¹, seq i â‰¤ m)

variables {Î± Î² Î¹ : Type*} {m : measurable_space Î±}

instance [preorder Î¹] : has_coe_to_fun (filtration Î¹ m) (Î» _, Î¹ â†’ measurable_space Î±) :=
âŸ¨Î» f, f.seqâŸ©

namespace filtration
variables [preorder Î¹]

protected lemma mono {i j : Î¹} (f : filtration Î¹ m) (hij : i â‰¤ j) : f i â‰¤ f j := f.mono' hij

protected lemma le (f : filtration Î¹ m) (i : Î¹) : f i â‰¤ m := f.le' i

@[ext] protected lemma ext {f g : filtration Î¹ m} (h : (f : Î¹ â†’ measurable_space Î±) = g) : f = g :=
by { cases f, cases g, simp only, exact h, }

variable (Î¹)
/-- The constant filtration which is equal to `m` for all `i : Î¹`. -/
def const (m' : measurable_space Î±) (hm' : m' â‰¤ m) : filtration Î¹ m :=
âŸ¨Î» _, m', monotone_const, Î» _, hm'âŸ©
variable {Î¹}

@[simp]
lemma const_apply {m' : measurable_space Î±} {hm' : m' â‰¤ m} (i : Î¹) : const Î¹ m' hm' i = m' := rfl

instance : inhabited (filtration Î¹ m) := âŸ¨const Î¹ m le_rflâŸ©

instance : has_le (filtration Î¹ m) := âŸ¨Î» f g, âˆ€ i, f i â‰¤ g iâŸ©

instance : has_bot (filtration Î¹ m) := âŸ¨const Î¹ âŠ¥ bot_leâŸ©

instance : has_top (filtration Î¹ m) := âŸ¨const Î¹ m le_rflâŸ©

instance : has_sup (filtration Î¹ m) := âŸ¨Î» f g,
{ seq   := Î» i, f i âŠ” g i,
  mono' := Î» i j hij, sup_le ((f.mono hij).trans le_sup_left) ((g.mono hij).trans le_sup_right),
  le'   := Î» i, sup_le (f.le i) (g.le i) }âŸ©

@[norm_cast] lemma coe_fn_sup {f g : filtration Î¹ m} : â‡‘(f âŠ” g) = f âŠ” g := rfl

instance : has_inf (filtration Î¹ m) := âŸ¨Î» f g,
{ seq   := Î» i, f i âŠ“ g i,
  mono' := Î» i j hij, le_inf (inf_le_left.trans (f.mono hij)) (inf_le_right.trans (g.mono hij)),
  le'   := Î» i, inf_le_left.trans (f.le i) }âŸ©

@[norm_cast] lemma coe_fn_inf {f g : filtration Î¹ m} : â‡‘(f âŠ“ g) = f âŠ“ g := rfl

instance : has_Sup (filtration Î¹ m) := âŸ¨Î» s,
{ seq   := Î» i, Sup ((Î» f : filtration Î¹ m, f i) '' s),
  mono' := Î» i j hij,
  begin
    refine Sup_le (Î» m' hm', _),
    rw [set.mem_image] at hm',
    obtain âŸ¨f, hf_mem, hfm'âŸ© := hm',
    rw â† hfm',
    refine (f.mono hij).trans _,
    have hfj_mem : f j âˆˆ ((Î» g : filtration Î¹ m, g j) '' s), from âŸ¨f, hf_mem, rflâŸ©,
    exact le_Sup hfj_mem,
  end,
  le'   := Î» i,
  begin
    refine Sup_le (Î» m' hm', _),
    rw [set.mem_image] at hm',
    obtain âŸ¨f, hf_mem, hfm'âŸ© := hm',
    rw â† hfm',
    exact f.le i,
  end, }âŸ©

lemma Sup_def (s : set (filtration Î¹ m)) (i : Î¹) :
  Sup s i = Sup ((Î» f : filtration Î¹ m, f i) '' s) :=
rfl

noncomputable
instance : has_Inf (filtration Î¹ m) := âŸ¨Î» s,
{ seq   := Î» i, if set.nonempty s then Inf ((Î» f : filtration Î¹ m, f i) '' s) else m,
  mono' := Î» i j hij,
  begin
    by_cases h_nonempty : set.nonempty s,
    swap, { simp only [h_nonempty, set.nonempty_image_iff, if_false, le_refl], },
    simp only [h_nonempty, if_true, le_Inf_iff, set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iffâ‚‚],
    refine Î» f hf_mem, le_trans _ (f.mono hij),
    have hfi_mem : f i âˆˆ ((Î» g : filtration Î¹ m, g i) '' s), from âŸ¨f, hf_mem, rflâŸ©,
    exact Inf_le hfi_mem,
  end,
  le'   := Î» i,
  begin
    by_cases h_nonempty : set.nonempty s,
    swap, { simp only [h_nonempty, if_false, le_refl], },
    simp only [h_nonempty, if_true],
    obtain âŸ¨f, hf_memâŸ© := h_nonempty,
    exact le_trans (Inf_le âŸ¨f, hf_mem, rflâŸ©) (f.le i),
  end, }âŸ©

lemma Inf_def (s : set (filtration Î¹ m)) (i : Î¹) :
  Inf s i = if set.nonempty s then Inf ((Î» f : filtration Î¹ m, f i) '' s) else m :=
rfl

noncomputable
instance : complete_lattice (filtration Î¹ m) :=
{ le           := (â‰¤),
  le_refl      := Î» f i, le_rfl,
  le_trans     := Î» f g h h_fg h_gh i, (h_fg i).trans (h_gh i),
  le_antisymm  := Î» f g h_fg h_gf, filtration.ext $ funext $ Î» i, (h_fg i).antisymm (h_gf i),
  sup          := (âŠ”),
  le_sup_left  := Î» f g i, le_sup_left,
  le_sup_right := Î» f g i, le_sup_right,
  sup_le       := Î» f g h h_fh h_gh i, sup_le (h_fh i) (h_gh _),
  inf          := (âŠ“),
  inf_le_left  := Î» f g i, inf_le_left,
  inf_le_right := Î» f g i, inf_le_right,
  le_inf       := Î» f g h h_fg h_fh i, le_inf (h_fg i) (h_fh i),
  Sup          := Sup,
  le_Sup       := Î» s f hf_mem i, le_Sup âŸ¨f, hf_mem, rflâŸ©,
  Sup_le       := Î» s f h_forall i, Sup_le $ Î» m' hm',
  begin
    obtain âŸ¨g, hg_mem, hfm'âŸ© := hm',
    rw â† hfm',
    exact h_forall g hg_mem i,
  end,
  Inf          := Inf,
  Inf_le       := Î» s f hf_mem i,
  begin
    have hs : s.nonempty := âŸ¨f, hf_memâŸ©,
    simp only [Inf_def, hs, if_true],
    exact Inf_le âŸ¨f, hf_mem, rflâŸ©,
  end,
  le_Inf       := Î» s f h_forall i,
  begin
    by_cases hs : s.nonempty,
    swap, { simp only [Inf_def, hs, if_false], exact f.le i, },
    simp only [Inf_def, hs, if_true, le_Inf_iff, set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iffâ‚‚],
    exact Î» g hg_mem, h_forall g hg_mem i,
  end,
  top          := âŠ¤,
  bot          := âŠ¥,
  le_top       := Î» f i, f.le' i,
  bot_le       := Î» f i, bot_le, }

end filtration

lemma measurable_set_of_filtration [preorder Î¹] {f : filtration Î¹ m} {s : set Î±} {i : Î¹}
  (hs : measurable_set[f i] s) : measurable_set[m] s :=
f.le i s hs

/-- A measure is Ïƒ-finite with respect to filtration if it is Ïƒ-finite with respect
to all the sub-Ïƒ-algebra of the filtration. -/
class sigma_finite_filtration [preorder Î¹] (Î¼ : measure Î±) (f : filtration Î¹ m) : Prop :=
(sigma_finite : âˆ€ i : Î¹, sigma_finite (Î¼.trim (f.le i)))

instance sigma_finite_of_sigma_finite_filtration [preorder Î¹] (Î¼ : measure Î±) (f : filtration Î¹ m)
  [hf : sigma_finite_filtration Î¼ f] (i : Î¹) :
  sigma_finite (Î¼.trim (f.le i)) :=
by apply hf.sigma_finite -- can't exact here

variable [measurable_space Î²]

/-- A sequence of functions `u` is adapted to a filtration `f` if for all `i`,
`u i` is `f i`-measurable. -/
def adapted [preorder Î¹] (f : filtration Î¹ m) (u : Î¹ â†’ Î± â†’ Î²) : Prop :=
âˆ€ i : Î¹, measurable[f i] (u i)

namespace adapted
variables [preorder Î¹]

lemma add [has_add Î²] [has_measurable_addâ‚‚ Î²] {u v : Î¹ â†’ Î± â†’ Î²} {f : filtration Î¹ m}
  (hu : adapted f u) (hv : adapted f v) : adapted f (u + v) :=
Î» i, @measurable.add _ _ _ _ (f i) _ _ _ (hu i) (hv i)

lemma neg [has_neg Î²] [has_measurable_neg Î²] {u : Î¹ â†’ Î± â†’ Î²} {f : filtration Î¹ m}
  (hu : adapted f u) : adapted f (-u) :=
Î» i, @measurable.neg _ Î± _ _ _ (f i) _ (hu i)

lemma smul [has_scalar â„ Î²] [has_measurable_smul â„ Î²] {u : Î¹ â†’ Î± â†’ Î²} {f : filtration Î¹ m}
  (c : â„) (hu : adapted f u) : adapted f (c â€¢ u) :=
Î» i, @measurable.const_smul â„ Î² Î± _ _ _ (f i) _ _ (hu i) c

end adapted

variable (Î²)

lemma adapted_zero [preorder Î¹] [has_zero Î²] (f : filtration Î¹ m) : adapted f (0 : Î¹ â†’ Î± â†’ Î²) :=
Î» i, @measurable_zero Î² Î± (f i) _ _

variable {Î²}

namespace filtration

/-- Given a sequence of functions, the natural filtration is the smallest sequence
of Ïƒ-algebras such that that sequence of functions is measurable with respect to
the filtration. -/
def natural [preorder Î¹] (u : Î¹ â†’ Î± â†’ Î²) (hum : âˆ€ i, measurable (u i)) : filtration Î¹ m :=
{ seq   := Î» i, â¨† j â‰¤ i, measurable_space.comap (u j) infer_instance,
  mono' := Î» i j hij, bsupr_le_bsupr' $ Î» k hk, le_trans hk hij,
  le'   := Î» i, bsupr_le (Î» j hj s hs, let âŸ¨t, ht, ht'âŸ© := hs in ht' â–¸ hum j ht) }

lemma adapted_natural [preorder Î¹] {u : Î¹ â†’ Î± â†’ Î²} (hum : âˆ€ i, measurable[m] (u i)) :
  adapted (natural u hum) u :=
Î» i, measurable.le (le_bsupr_of_le i (le_refl i) le_rfl) (Î» s hs, âŸ¨s, hs, rflâŸ©)

lemma is_max.mono [preorder Î¹] {i j : Î¹} (hi : is_max i) (hij : i â‰¤ j) : is_max j :=
Î» k hjk, (hi (hij.trans hjk)).trans hij

section right_continuous_filtration
variables [partial_order Î¹]

/-- TODO
The choice of the value `f i` if `i` is a maximal element is such that `right_continuous_filtration`
sends a constant filtration to itself. -/
noncomputable
def right_continuous_filtration (f : filtration Î¹ m) : filtration Î¹ m :=
{ seq   := Î» i, if is_max i then (f i) else (â¨… j > i, f j),
  mono' := Î» i j hij,
  begin
    by_cases hi : is_max i,
    { have hj : is_max j, from Î» k hk, (hi (hij.trans hk)).trans hij,
      simp only [hi, hj, gt_iff_lt, if_true, f.mono hij], },
    { by_cases hj : is_max j,
      { simp only [hi, hj, if_true, if_false, gt_iff_lt],
        have hi_lt_j : i < j, from lt_of_le_of_ne hij (Î» hi_eq_j, hi (hi_eq_j.symm â–¸ hj)),
        exact binfi_le j hi_lt_j, },
      { simp only [hi, hj, if_false],
        exact infi_le_infi_of_subset (Î» k hk_lt_j, hij.trans_lt hk_lt_j), }, },
  end,
  le'   := Î» i,
  begin
    by_cases hi : is_max i,
    { simp only [hi, if_true, f.le i], },
    { simp only [hi, if_false],
      obtain âŸ¨k, hkâŸ© := not_is_max_iff.mp hi,
      exact (binfi_le k hk).trans (f.le k), },
  end }

lemma right_continuous_filtration_def (f : filtration Î¹ m) (i : Î¹) :
  right_continuous_filtration f i = if is_max i then (f i) else (â¨… j > i, f j) := rfl

lemma right_continuous_filtration_eq_infi [no_max_order Î¹] (f : filtration Î¹ m) (i : Î¹) :
  right_continuous_filtration f i = â¨… j > i, f j :=
by simp only [right_continuous_filtration_def, not_is_max i, if_false]

lemma right_continuous_filtration_succ_order [succ_order Î¹]
  (f : filtration Î¹ m) (i : Î¹) (hi : Â¬ is_max i) :
  right_continuous_filtration f i = f (succ_order.succ i) :=
begin
  simp_rw [right_continuous_filtration_def, hi, if_false],
  refine le_antisymm (binfi_le _ (succ_order.lt_succ_of_not_is_max hi))
    (le_binfi (Î» j hi_lt_j, f.mono _)),
  rw succ_order.succ_le_iff_of_not_is_max hi,
  exact hi_lt_j,
end

lemma le_right_continuous_filtration (f : filtration Î¹ m) : f â‰¤ right_continuous_filtration f :=
begin
  intro i,
  by_cases hi : is_max i,
  { simp only [right_continuous_filtration_def f i, hi, if_true, le_rfl], },
  { simp only [right_continuous_filtration_def f i, hi, if_false],
    exact le_binfi (Î» j hij, f.mono hij.lt.le), },
end

/-- `right_continuous_filtration` is idempotent.
Note the `densely_ordered Î¹` assumption: this is not true in general. If `Î¹ = {0, 1, 2}` with
`0 < 1 < 2` then `right_continuous_filtration f 0 = f 1`, `right_continuous_filtration f 1 = f 2`,
and then `right_continuous_filtration (right_continuous_filtration f) 0 = f 2`. -/
lemma right_continuous_filtration_idempotent [densely_ordered Î¹] (f : filtration Î¹ m) :
  right_continuous_filtration (right_continuous_filtration f) = right_continuous_filtration f :=
begin
  refine le_antisymm (Î» i, _) (le_right_continuous_filtration _),
  by_cases hi : is_max i,
  { simp only [right_continuous_filtration_def, hi, if_true, le_rfl], },
  simp only [right_continuous_filtration_def, hi, if_false],
  refine le_binfi (Î» j hi_lt_j, _),
  obtain âŸ¨n, hi_lt_n, hn_lt_jâŸ© : âˆƒ n, i < n âˆ§ n < j, from exists_between hi_lt_j,
  refine (binfi_le n hi_lt_n).trans _,
  have h_not_max : Â¬ is_max n, from not_is_max_iff.mpr âŸ¨j, hn_lt_jâŸ©,
  simp only [h_not_max, if_false],
  exact binfi_le j hn_lt_j,
end

/-- A filtration is right continuous if `f = right_continuous_filtration f`. -/
def is_right_continuous (f : filtration Î¹ m) : Prop := f = right_continuous_filtration f

lemma is_right_continuous_const {m0 : measurable_space Î±} (hm : m0 â‰¤ m) :
  is_right_continuous (const Î¹ m0 hm) :=
begin
  ext1,
  ext1 i,
  by_cases hi : is_max i,
  { simp_rw [right_continuous_filtration_def, const_apply, hi, if_true], },
  simp_rw [right_continuous_filtration_def, const_apply, hi, if_false],
  refine le_antisymm (le_binfi (Î» j hi_lt_j, le_rfl)) _,
  obtain âŸ¨k, hi_lt_kâŸ© := not_is_max_iff.mp hi,
  exact binfi_le k hi_lt_k,
end

lemma is_right_continuous_right_continuous_filtration [densely_ordered Î¹] (f : filtration Î¹ m) :
  is_right_continuous (right_continuous_filtration f) :=
(right_continuous_filtration_idempotent f).symm

end right_continuous_filtration

end filtration

/-- A stopping time with respect to some filtration `f` is a function
`Ï„` such that for all `i`, the preimage of `{j | j â‰¤ i}` along `Ï„` is measurable
with respect to `f i`.

Intuitively, the stopping time `Ï„` describes some stopping rule such that at time
`i`, we may determine it with the information we have at time `i`. -/
def is_stopping_time [preorder Î¹] (f : filtration Î¹ m) (Ï„ : Î± â†’ Î¹) :=
âˆ€ i : Î¹, measurable_set[f i] $ {x | Ï„ x â‰¤ i}

lemma is_stopping_time_const [preorder Î¹] {f : filtration Î¹ m} (i : Î¹) :
  is_stopping_time f (Î» x, i) :=
Î» j, by simp only [measurable_set.const]

section measurable_set

section preorder
variables [preorder Î¹] {f : filtration Î¹ m} {Ï„ : Î± â†’ Î¹}

lemma is_stopping_time.measurable_set_le (hÏ„ : is_stopping_time f Ï„) (i : Î¹) :
  measurable_set[f i] {x | Ï„ x â‰¤ i} :=
hÏ„ i

lemma is_stopping_time.measurable_set_lt_of_pred [pred_order Î¹]
  (hÏ„ : is_stopping_time f Ï„) (i : Î¹) :
  measurable_set[f i] {x | Ï„ x < i} :=
begin
  by_cases hi_min : is_min i,
  { suffices : {x : Î± | Ï„ x < i} = âˆ…, by { rw this, exact @measurable_set.empty _ (f i), },
    ext1 x,
    simp only [set.mem_set_of_eq, set.mem_empty_eq, iff_false],
    rw is_min_iff_forall_not_lt at hi_min,
    exact hi_min (Ï„ x), },
  have : {x : Î± | Ï„ x < i} = Ï„ â»Â¹' (set.Iio i),
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_preimage, set.mem_Iio], },
  rw [this, pred_order.Iio_eq_Iic_pred' hi_min],
  have : Ï„ â»Â¹' set.Iic (pred_order.pred i) = {x : Î± | Ï„ x â‰¤ pred_order.pred i},
  { ext1 x, simp only [set.mem_preimage, set.mem_Iic, set.mem_set_of_eq], },
  rw this,
  exact f.mono (pred_order.pred_le i) _ (hÏ„.measurable_set_le (pred_order.pred i)),
end

/-- This lemma only says that the set is measurable because it's empty. This is an auxiliary result
for `is_stopping_time.measurbale_set_lt`. -/
lemma is_measurable_lt_of_is_min {i : Î¹} (hi : is_min i) (m : measurable_space Î±) :
  measurable_set[m] {x | Ï„ x < i} :=
begin
  suffices : {x : Î± | Ï„ x < i} = âˆ…, by { rw this, exact @measurable_set.empty _ m, },
  ext1 x,
  simp only [set.mem_set_of_eq, set.mem_empty_eq, iff_false],
  rw is_min_iff_forall_not_lt at hi,
  exact hi (Ï„ x),
end

end preorder

lemma lub_Iio_le {Î¹} [preorder Î¹] {j : Î¹} (i : Î¹) (hj : is_lub (set.Iio i) j) : j â‰¤ i :=
by { rw is_lub_le_iff hj, exact Î» k hk, le_of_lt hk, }

lemma todo {Î¹} [partial_order Î¹] {j : Î¹} (i : Î¹) (hj : is_lub (set.Iio i) j) :
  j = i âˆ¨ set.Iio i = set.Iic j :=
begin
  cases eq_or_lt_of_le (lub_Iio_le i hj) with hj_eq_i hj_lt_i,
  { exact or.inl hj_eq_i, },
  { right,
    ext1 k,
    refine âŸ¨Î» hk_lt, hj.1 hk_lt, Î» hk_le_j, lt_of_le_of_lt hk_le_j hj_lt_iâŸ©, },
end

lemma exists_lub_Iio [linear_order Î¹] (i : Î¹) : âˆƒ j, is_lub (set.Iio i) j :=
begin
  by_cases h_exists_lt : âˆƒ j, j âˆˆ upper_bounds (set.Iio i) âˆ§ j < i,
  { obtain âŸ¨j, hj_ub, hj_lt_iâŸ© := h_exists_lt,
    refine âŸ¨j, hj_ub, Î» k hk_ub, hk_ub hj_lt_iâŸ©, },
  { refine âŸ¨i, Î» j hj, le_of_lt hj, _âŸ©,
    by_contra,
    refine h_exists_lt _,
    rw mem_lower_bounds at h,
    push_neg at h,
    exact h, },
end

section linear_order

variables [linear_order Î¹] {f : filtration Î¹ m} {Ï„ : Î± â†’ Î¹}

lemma is_stopping_time.measurable_set_gt (hÏ„ : is_stopping_time f Ï„) (i : Î¹) :
  measurable_set[f i] {x | i < Ï„ x} :=
begin
  have : {x | i < Ï„ x} = {x | Ï„ x â‰¤ i}á¶œ,
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_compl_eq, not_le], },
  rw this,
  exact (hÏ„.measurable_set_le i).compl,
end

variables [topological_space Î¹] [order_topology Î¹] [first_countable_topology Î¹]

/-- Auxiliary lemma for `is_stopping_time.measurable_set_lt`. -/
lemma is_stopping_time.measurable_set_lt_of_is_lub
  (hÏ„ : is_stopping_time f Ï„) (i : Î¹) (h_lub : is_lub (set.Iio i) i) :
  measurable_set[f i] {x | Ï„ x < i} :=
begin
  by_cases hi_min : is_min i,
  { exact is_measurable_lt_of_is_min hi_min (f i), },
  have h_nonempty : (set.Iio i).nonempty, from not_is_min_iff.mp hi_min,
  obtain âŸ¨n, hniâŸ© := not_is_min_iff.mp hi_min,
  obtain âŸ¨seq, _, h_bound, h_tendstoâŸ© :
    âˆƒ seq : â„• â†’ Î¹, monotone seq âˆ§ (âˆ€ j, seq j < i) âˆ§ tendsto seq at_top (ð“ i),
  { obtain âŸ¨seq, h_mono, h_lt, h_tendsto, h_memâŸ© :=
      h_lub.exists_seq_monotone_tendsto h_nonempty,
    exact âŸ¨seq, h_mono, (Î» j, h_mem j), h_tendstoâŸ©, },
  have h_Ioi_eq_Union : set.Iio i = â‹ƒ j, { k | k â‰¤ seq j},
  { ext1 k,
    simp only [set.mem_Iio, set.mem_Union, set.mem_set_of_eq],
    refine âŸ¨Î» hk_lt_i, _, Î» h_exists_k_le_seq, _âŸ©,
    { rw tendsto_at_top' at h_tendsto,
      have h_nhds : set.Ici k âˆˆ ð“ i,
        from mem_nhds_iff.mpr âŸ¨set.Ioi k, set.Ioi_subset_Ici le_rfl, is_open_Ioi, hk_lt_iâŸ©,
      obtain âŸ¨a, haâŸ© : âˆƒ (a : â„•), âˆ€ (b : â„•), b â‰¥ a â†’ k â‰¤ seq b := h_tendsto (set.Ici k) h_nhds,
      exact âŸ¨a, ha a le_rflâŸ©, },
    { obtain âŸ¨j, hk_seq_jâŸ© := h_exists_k_le_seq,
      exact hk_seq_j.trans_lt (h_bound j), }, },
  have h_lt_eq_preimage : {x : Î± | Ï„ x < i} = Ï„ â»Â¹' (set.Iio i),
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_preimage, set.mem_Iio], },
  rw [h_lt_eq_preimage, h_Ioi_eq_Union],
  simp only [set.preimage_Union, set.preimage_set_of_eq],
  exact measurable_set.Union
    (Î» n, f.mono (h_bound n).le _ (hÏ„.measurable_set_le (seq n))),
end

lemma is_stopping_time.measurable_set_lt (hÏ„ : is_stopping_time f Ï„) (i : Î¹) :
  measurable_set[f i] {x | Ï„ x < i} :=
begin
  obtain âŸ¨i', hi'_lubâŸ© : âˆƒ i', is_lub (set.Iio i) i', from exists_lub_Iio i,
  cases todo i hi'_lub with hi'_eq_i h_Iio_eq_Iic,
  { rw â† hi'_eq_i at hi'_lub âŠ¢,
    exact hÏ„.measurable_set_lt_of_is_lub i' hi'_lub, },
  { have h_lt_eq_preimage : {x : Î± | Ï„ x < i} = Ï„ â»Â¹' (set.Iio i) := rfl,
    rw [h_lt_eq_preimage, h_Iio_eq_Iic],
    exact f.mono (lub_Iio_le i hi'_lub) _ (hÏ„.measurable_set_le i'), },
end

lemma is_stopping_time.measurable_set_ge (hÏ„ : is_stopping_time f Ï„) (i : Î¹) :
  measurable_set[f i] {x | i â‰¤ Ï„ x} :=
begin
  have : {x | i â‰¤ Ï„ x} = {x | Ï„ x < i}á¶œ,
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_compl_eq, not_lt], },
  rw this,
  exact (hÏ„.measurable_set_lt i).compl,
end

lemma is_stopping_time.measurable_set_eq (hÏ„ : is_stopping_time f Ï„) (i : Î¹) :
  measurable_set[f i] {x | Ï„ x = i} :=
begin
  have : {x | Ï„ x = i} = {x | Ï„ x â‰¤ i} âˆ© {x | Ï„ x â‰¥ i},
  { ext1 x, simp only [set.mem_set_of_eq, ge_iff_le, set.mem_inter_eq, le_antisymm_iff], },
  rw this,
  exact (hÏ„.measurable_set_le i).inter (hÏ„.measurable_set_ge i),
end

end linear_order

end measurable_set

section nat
variables {f : filtration â„• m} {Ï„ : Î± â†’ â„•}

lemma is_stopping_time.measurable_set_eq_le
  {f : filtration â„• m} {Ï„ : Î± â†’ â„•} (hÏ„ : is_stopping_time f Ï„) {i j : â„•} (hle : i â‰¤ j) :
  measurable_set[f j] {x | Ï„ x = i} :=
f.mono hle _ $ hÏ„.measurable_set_eq i

lemma is_stopping_time.measurable_set_lt_le
  (hÏ„ : is_stopping_time f Ï„) {i j : â„•} (hle : i â‰¤ j) :
  measurable_set[f j] {x | Ï„ x < i} :=
f.mono hle _ $ hÏ„.measurable_set_lt i

lemma is_stopping_time_of_measurable_set_eq
  {f : filtration â„• m} {Ï„ : Î± â†’ â„•} (hÏ„ : âˆ€ i, measurable_set[f i] {x | Ï„ x = i}) :
  is_stopping_time f Ï„ :=
begin
  intro i,
  rw show {x | Ï„ x â‰¤ i} = â‹ƒ k â‰¤ i, {x | Ï„ x = k}, by { ext, simp },
  refine measurable_set.bUnion (set.countable_encodable _) (Î» k hk, _),
  exact f.mono hk _ (hÏ„ k),
end

end nat

namespace is_stopping_time

lemma max [linear_order Î¹] {f : filtration Î¹ m} {Ï„ Ï€ : Î± â†’ Î¹}
  (hÏ„ : is_stopping_time f Ï„) (hÏ€ : is_stopping_time f Ï€) :
  is_stopping_time f (Î» x, max (Ï„ x) (Ï€ x)) :=
begin
  intro i,
  simp_rw [max_le_iff, set.set_of_and],
  exact (hÏ„ i).inter (hÏ€ i),
end

lemma min [linear_order Î¹] {f : filtration Î¹ m} {Ï„ Ï€ : Î± â†’ Î¹}
  (hÏ„ : is_stopping_time f Ï„) (hÏ€ : is_stopping_time f Ï€) :
  is_stopping_time f (Î» x, min (Ï„ x) (Ï€ x)) :=
begin
  intro i,
  simp_rw [min_le_iff, set.set_of_or],
  exact (hÏ„ i).union (hÏ€ i),
end

lemma add_const [add_group Î¹] [preorder Î¹] [covariant_class Î¹ Î¹ (function.swap (+)) (â‰¤)]
  [covariant_class Î¹ Î¹ (+) (â‰¤)]
  {f : filtration Î¹ m} {Ï„ : Î± â†’ Î¹} (hÏ„ : is_stopping_time f Ï„) {i : Î¹} (hi : 0 â‰¤ i) :
  is_stopping_time f (Î» x, Ï„ x + i) :=
begin
  intro j,
  simp_rw [â† le_sub_iff_add_le],
  exact f.mono (sub_le_self j hi) _ (hÏ„ (j - i)),
end

section preorder

variables [preorder Î¹] {f : filtration Î¹ m}

/-- The associated Ïƒ-algebra with a stopping time. -/
protected def measurable_space
  {Ï„ : Î± â†’ Î¹} (hÏ„ : is_stopping_time f Ï„) : measurable_space Î± :=
{ measurable_set' := Î» s, âˆ€ i : Î¹, measurable_set[f i] (s âˆ© {x | Ï„ x â‰¤ i}),
  measurable_set_empty :=
    Î» i, (set.empty_inter {x | Ï„ x â‰¤ i}).symm â–¸ @measurable_set.empty _ (f i),
  measurable_set_compl := Î» s hs i,
    begin
      rw (_ : sá¶œ âˆ© {x | Ï„ x â‰¤ i} = (sá¶œ âˆª {x | Ï„ x â‰¤ i}á¶œ) âˆ© {x | Ï„ x â‰¤ i}),
      { refine measurable_set.inter _ _,
        { rw â† set.compl_inter,
          exact (hs i).compl },
        { exact hÏ„ i} },
      { rw set.union_inter_distrib_right,
        simp only [set.compl_inter_self, set.union_empty] }
    end,
  measurable_set_Union := Î» s hs i,
    begin
      rw forall_swap at hs,
      rw set.Union_inter,
      exact measurable_set.Union (hs i),
    end }

@[protected]
lemma measurable_set {Ï„ : Î± â†’ Î¹} (hÏ„ : is_stopping_time f Ï„) (s : set Î±) :
  measurable_set[hÏ„.measurable_space] s â†”
  âˆ€ i : Î¹, measurable_set[f i] (s âˆ© {x | Ï„ x â‰¤ i}) :=
iff.rfl

lemma measurable_space_mono
  {Ï„ Ï€ : Î± â†’ Î¹} (hÏ„ : is_stopping_time f Ï„) (hÏ€ : is_stopping_time f Ï€) (hle : Ï„ â‰¤ Ï€) :
  hÏ„.measurable_space â‰¤ hÏ€.measurable_space :=
begin
  intros s hs i,
  rw (_ : s âˆ© {x | Ï€ x â‰¤ i} = s âˆ© {x | Ï„ x â‰¤ i} âˆ© {x | Ï€ x â‰¤ i}),
  { exact (hs i).inter (hÏ€ i) },
  { ext,
    simp only [set.mem_inter_eq, iff_self_and, and.congr_left_iff, set.mem_set_of_eq],
    intros hle' _,
    exact le_trans (hle _) hle' },
end

lemma measurable_space_le [encodable Î¹] {Ï„ : Î± â†’ Î¹} (hÏ„ : is_stopping_time f Ï„) :
  hÏ„.measurable_space â‰¤ m :=
begin
  intros s hs,
  change âˆ€ i, measurable_set[f i] (s âˆ© {x | Ï„ x â‰¤ i}) at hs,
  rw (_ : s = â‹ƒ i, s âˆ© {x | Ï„ x â‰¤ i}),
  { exact measurable_set.Union (Î» i, f.le i _ (hs i)) },
  { ext x, split; rw set.mem_Union,
    { exact Î» hx, âŸ¨Ï„ x, hx, le_rflâŸ© },
    { rintro âŸ¨_, hx, _âŸ©,
      exact hx } }
end

section nat

lemma measurable_set_eq_const {f : filtration â„• m}
  {Ï„ : Î± â†’ â„•} (hÏ„ : is_stopping_time f Ï„) (i : â„•) :
  measurable_set[hÏ„.measurable_space] {x | Ï„ x = i} :=
begin
  rw hÏ„.measurable_set,
  intro j,
  by_cases i â‰¤ j,
  { rw (_ : {x | Ï„ x = i} âˆ© {x | Ï„ x â‰¤ j} = {x | Ï„ x = i}),
    { exact hÏ„.measurable_set_eq_le h },
    { ext,
      simp only [set.mem_inter_eq, and_iff_left_iff_imp, set.mem_set_of_eq],
      rintro rfl,
      assumption } },
  { rw (_ : {x | Ï„ x = i} âˆ© {x | Ï„ x â‰¤ j} = âˆ…),
    { exact @measurable_set.empty _ (f j) },
    { ext,
      simp only [set.mem_empty_eq, set.mem_inter_eq, not_and, not_le, set.mem_set_of_eq, iff_false],
      rintro rfl,
      rwa not_le at h } }
end

end nat

end preorder

section linear_order

variable [linear_order Î¹]

lemma measurable [topological_space Î¹] [measurable_space Î¹]
  [borel_space Î¹] [order_topology Î¹] [second_countable_topology Î¹]
  {f : filtration Î¹ m} {Ï„ : Î± â†’ Î¹} (hÏ„ : is_stopping_time f Ï„) :
  measurable[hÏ„.measurable_space] Ï„ :=
begin
  refine @measurable_of_Iic Î¹ Î± _ _ _ hÏ„.measurable_space _ _ _ _ _,
  simp_rw [hÏ„.measurable_set, set.preimage, set.mem_Iic],
  intros i j,
  rw (_ : {x | Ï„ x â‰¤ i} âˆ© {x | Ï„ x â‰¤ j} = {x | Ï„ x â‰¤ linear_order.min i j}),
  { exact f.mono (min_le_right i j) _ (hÏ„ (linear_order.min i j)) },
  { ext,
    simp only [set.mem_inter_eq, iff_self, le_min_iff, set.mem_set_of_eq] }
end

end linear_order

end is_stopping_time

section linear_order

/-- Given a map `u : Î¹ â†’ Î± â†’ E`, its stopped value with respect to the stopping
time `Ï„` is the map `x â†¦ u (Ï„ x) x`. -/
def stopped_value (u : Î¹ â†’ Î± â†’ Î²) (Ï„ : Î± â†’ Î¹) : Î± â†’ Î² :=
Î» x, u (Ï„ x) x

variable [linear_order Î¹]

/-- Given a map `u : Î¹ â†’ Î± â†’ E`, the stopped process with respect to `Ï„` is `u i x` if
`i â‰¤ Ï„ x`, and `u (Ï„ x) x` otherwise.

Intuitively, the stopped process stops evolving once the stopping time has occured. -/
def stopped_process (u : Î¹ â†’ Î± â†’ Î²) (Ï„ : Î± â†’ Î¹) : Î¹ â†’ Î± â†’ Î² :=
Î» i x, u (linear_order.min i (Ï„ x)) x

lemma stopped_process_eq_of_le {u : Î¹ â†’ Î± â†’ Î²} {Ï„ : Î± â†’ Î¹}
  {i : Î¹} {x : Î±} (h : i â‰¤ Ï„ x) : stopped_process u Ï„ i x = u i x :=
by simp [stopped_process, min_eq_left h]

lemma stopped_process_eq_of_ge {u : Î¹ â†’ Î± â†’ Î²} {Ï„ : Î± â†’ Î¹}
  {i : Î¹} {x : Î±} (h : Ï„ x â‰¤ i) : stopped_process u Ï„ i x = u (Ï„ x) x :=
by simp [stopped_process, min_eq_right h]

-- We will need cadlag to generalize the following to continuous processes
section nat

open filtration

variables {f : filtration â„• m} {u : â„• â†’ Î± â†’ Î²} {Ï„ Ï€ : Î± â†’ â„•}

lemma stopped_value_sub_eq_sum [add_comm_group Î²] (hle : Ï„ â‰¤ Ï€) :
  stopped_value u Ï€ - stopped_value u Ï„ =
  Î» x, (âˆ‘ i in finset.Ico (Ï„ x) (Ï€ x), (u (i + 1) - u i)) x :=
begin
  ext x,
  rw [finset.sum_Ico_eq_sub _ (hle x), finset.sum_range_sub, finset.sum_range_sub],
  simp [stopped_value],
end

lemma stopped_value_sub_eq_sum' [add_comm_group Î²] (hle : Ï„ â‰¤ Ï€) {N : â„•} (hbdd : âˆ€ x, Ï€ x â‰¤ N) :
  stopped_value u Ï€ - stopped_value u Ï„ =
  Î» x, (âˆ‘ i in finset.range (N + 1),
    set.indicator {x | Ï„ x â‰¤ i âˆ§ i < Ï€ x} (u (i + 1) - u i)) x :=
begin
  rw stopped_value_sub_eq_sum hle,
  ext x,
  simp only [finset.sum_apply, finset.sum_indicator_eq_sum_filter],
  refine finset.sum_congr _ (Î» _ _, rfl),
  ext i,
  simp only [finset.mem_filter, set.mem_set_of_eq, finset.mem_range, finset.mem_Ico],
  exact âŸ¨Î» h, âŸ¨lt_trans h.2 (nat.lt_succ_iff.2 $ hbdd _), hâŸ©, Î» h, h.2âŸ©
end

section add_comm_monoid

variables [add_comm_monoid Î²]

lemma stopped_value_eq {N : â„•} (hbdd : âˆ€ x, Ï„ x â‰¤ N) :
  stopped_value u Ï„ =
  Î» x, (âˆ‘ i in finset.range (N + 1), set.indicator {x | Ï„ x = i} (u i)) x :=
begin
  ext y,
  rw [stopped_value, finset.sum_apply, finset.sum_eq_single (Ï„ y)],
  { rw set.indicator_of_mem,
    exact rfl },
  { exact Î» i hi hneq, set.indicator_of_not_mem hneq.symm _ },
  { intro hy,
    rw set.indicator_of_not_mem,
    exact Î» _, hy (finset.mem_range.2 $ lt_of_le_of_lt (hbdd _) (nat.lt_succ_self _)) }
end

lemma stopped_process_eq (n : â„•) :
  stopped_process u Ï„ n =
  set.indicator {a | n â‰¤ Ï„ a} (u n) +
    âˆ‘ i in finset.range n, set.indicator {a | Ï„ a = i} (u i) :=
begin
  ext x,
  rw [pi.add_apply, finset.sum_apply],
  cases le_or_lt n (Ï„ x),
  { rw [stopped_process_eq_of_le h, set.indicator_of_mem, finset.sum_eq_zero, add_zero],
    { intros m hm,
      rw finset.mem_range at hm,
      exact set.indicator_of_not_mem ((lt_of_lt_of_le hm h).ne.symm) _ },
    { exact h } },
  { rw [stopped_process_eq_of_ge (le_of_lt h), finset.sum_eq_single_of_mem (Ï„ x)],
    { rw [set.indicator_of_not_mem, zero_add, set.indicator_of_mem],
      { exact rfl }, -- refl does not work
      { exact not_le.2 h } },
    { rwa [finset.mem_range] },
    { intros b hb hneq,
      rw set.indicator_of_not_mem,
      exact hneq.symm } },
end

lemma adapted.stopped_process [measurable_space Î²] [has_measurable_addâ‚‚ Î²]
  (hu : adapted f u) (hÏ„ : is_stopping_time f Ï„) :
  adapted f (stopped_process u Ï„) :=
begin
  intro i,
  rw stopped_process_eq,
  refine @measurable.add _ _ _ _ (f i) _ _ _ _ _,
  { refine (hu i).indicator _,
    convert measurable_set.union (hÏ„ i).compl (hÏ„.measurable_set_eq i),
    ext x,
    change i â‰¤ Ï„ x â†” Â¬ Ï„ x â‰¤ i âˆ¨ Ï„ x = i,
    rw [not_le, le_iff_lt_or_eq, eq_comm] },
  { refine @finset.measurable_sum' _ _ _ _ _ _ (f i) _ _ _,
    refine Î» j hij, measurable.indicator _ _,
    { rw finset.mem_range at hij,
      exact measurable.le (f.mono hij.le) (hu j) },
    { rw finset.mem_range at hij,
      refine f.mono hij.le _ _,
      convert hÏ„.measurable_set_eq j, } }
end

end add_comm_monoid

section normed_group

variables [measurable_space Î²] [normed_group Î²] [has_measurable_addâ‚‚ Î²]

lemma measurable_stopped_process (hÏ„ : is_stopping_time f Ï„) (hu : adapted f u) (n : â„•) :
  measurable (stopped_process u Ï„ n) :=
(hu.stopped_process hÏ„ n).le (f.le _)

lemma mem_â„’p_stopped_process {p : â„â‰¥0âˆž} [borel_space Î²] {Î¼ : measure Î±} (hÏ„ : is_stopping_time f Ï„)
  (hu : âˆ€ n, mem_â„’p (u n) p Î¼) (n : â„•) :
  mem_â„’p (stopped_process u Ï„ n) p Î¼ :=
begin
  rw stopped_process_eq,
  refine mem_â„’p.add _ _,
  { exact mem_â„’p.indicator (f.le n {a : Î± | n â‰¤ Ï„ a} (hÏ„.measurable_set_ge n)) (hu n) },
  { suffices : mem_â„’p (Î» x, âˆ‘ (i : â„•) in finset.range n, {a : Î± | Ï„ a = i}.indicator (u i) x) p Î¼,
    { convert this, ext1 x, simp only [finset.sum_apply] },
    refine mem_â„’p_finset_sum _ (Î» i hi, mem_â„’p.indicator _ (hu i)),
    exact f.le i {a : Î± | Ï„ a = i} (hÏ„.measurable_set_eq i) },
end

lemma integrable_stopped_process [borel_space Î²] {Î¼ : measure Î±} (hÏ„ : is_stopping_time f Ï„)
  (hu : âˆ€ n, integrable (u n) Î¼) (n : â„•) :
  integrable (stopped_process u Ï„ n) Î¼ :=
begin
  simp_rw â† mem_â„’p_one_iff_integrable at hu âŠ¢,
  exact mem_â„’p_stopped_process hÏ„ hu n,
end

lemma mem_â„’p_stopped_value {p : â„â‰¥0âˆž} [borel_space Î²] {Î¼ : measure Î±} (hÏ„ : is_stopping_time f Ï„)
  (hu : âˆ€ n, mem_â„’p (u n) p Î¼) {N : â„•} (hbdd : âˆ€ x, Ï„ x â‰¤ N) :
  mem_â„’p (stopped_value u Ï„) p Î¼ :=
begin
  rw stopped_value_eq hbdd,
  suffices : mem_â„’p (Î» x, âˆ‘ (i : â„•) in finset.range (N + 1),
    {a : Î± | Ï„ a = i}.indicator (u i) x) p Î¼,
  { convert this, ext1 x, simp only [finset.sum_apply] },
  refine mem_â„’p_finset_sum _ (Î» i hi, mem_â„’p.indicator _ (hu i)),
  exact f.le i {a : Î± | Ï„ a = i} (hÏ„.measurable_set_eq i)
end

lemma integrable_stopped_value [borel_space Î²] {Î¼ : measure Î±} (hÏ„ : is_stopping_time f Ï„)
  (hu : âˆ€ n, integrable (u n) Î¼) {N : â„•} (hbdd : âˆ€ x, Ï„ x â‰¤ N) :
  integrable (stopped_value u Ï„) Î¼ :=
begin
  simp_rw â† mem_â„’p_one_iff_integrable at hu âŠ¢,
  exact mem_â„’p_stopped_value hÏ„ hu hbdd,
end

end normed_group

end nat

end linear_order

end measure_theory
