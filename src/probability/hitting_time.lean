/-
Copyright (c) 2022 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/

import probability.notation
import probability.stopping

/-! # Hitting times -/

open_locale probability_theory measure_theory ennreal nnreal filter

open measure_theory topological_space

namespace probability_theory

variables {Î¹ Î± Î² : Type*} [complete_lattice Î¹] {mÎ± : measurable_space Î±} {Î¼ : measure Î±}
  {s t : set Î²} {f : Î¹ â†’ Î± â†’ Î²} {n : Î¹} {x : Î±}

/-- First time at which the random process `f` belongs to the set `s`. -/
def hitting_time (f : Î¹ â†’ Î± â†’ Î²) (s : set Î²) : Î± â†’ Î¹ :=
Î» x, Inf {i | f i x âˆˆ s}

@[simp] lemma hitting_time_univ : hitting_time f set.univ = Î» _, âŠ¥ := by simp [hitting_time]

@[simp] lemma hitting_time_empty : hitting_time f (âˆ… : set Î²) = Î» _, âŠ¤ := by simp [hitting_time]

lemma hitting_time_inter_ge_sup :
  hitting_time f s âŠ” hitting_time f t â‰¤ hitting_time f (s âˆ© t) :=
begin
  intro x,
  simp only [hitting_time, pi.sup_apply, set.mem_inter_eq, le_Inf_iff, set.mem_set_of_eq,
    sup_le_iff, and_imp],
  exact âŸ¨Î» i hixs hixt, Inf_le hixs, Î» i hixs hixt, Inf_le hixtâŸ©,
end

lemma hitting_time_union_le_inf :
  hitting_time f (s âˆª t) â‰¤ hitting_time f s âŠ“ hitting_time f t :=
begin
  intro x,
  simp only [hitting_time, pi.inf_apply, set.mem_union_eq, le_inf_iff, le_Inf_iff,
    set.mem_set_of_eq],
  exact âŸ¨Î» i hixs, Inf_le (or.inl hixs), Î» i hixt, Inf_le (or.inr hixt)âŸ©,
end

lemma exists_mem_of_hitting_time_lt_top (h : hitting_time f s x â‰  âŠ¤) : âˆƒ j, f j x âˆˆ s :=
begin
  by_contra' h_forall_nmem,
  simpa only [hitting_time, h_forall_nmem, set.set_of_false, Inf_empty, not_top_lt] using h,
end

lemma hitting_time_mem {Î¹} [complete_linear_order Î¹] [is_well_order Î¹ (<)]
  {f : Î¹ â†’ Î± â†’ Î²} (h : hitting_time f s x â‰  âŠ¤) :
  f (hitting_time f s x) x âˆˆ s :=
Inf_mem (exists_mem_of_hitting_time_lt_top h)

lemma hitting_time_le_iff {Î¹} [complete_linear_order Î¹] [is_well_order Î¹ (<)]
  {f : Î¹ â†’ Î± â†’ Î²} {n : Î¹} (hn : n â‰  âŠ¤) :
  hitting_time f s x â‰¤ n â†” âˆƒ j â‰¤ n, f j x âˆˆ s :=
âŸ¨Î» h, âŸ¨hitting_time f s x, h, hitting_time_mem (h.trans_lt hn.lt_top).neâŸ©,
  Î» h, (Inf_le (by exact h.some_spec.some_spec)).trans h.some_spec.someâŸ©

section nat

variables {ð’¢ : filtration (with_top â„•) mÎ±} {g : (with_top â„•) â†’ Î± â†’ Î²}

instance with_top.encodable {Î±} [encodable Î±] : encodable (with_top Î±) := encodable.option
instance with_bot.encodable {Î±} [encodable Î±] : encodable (with_bot Î±) := encodable.option

instance : is_well_order (with_top â„•) (<) := âŸ¨with_top.well_founded_lt nat.lt_wfâŸ©

lemma is_stopping_time_hitting_time [topological_space Î²] [metrizable_space Î²] [measurable_space Î²]
  [borel_space Î²]
  (hg : adapted ð’¢ g) (hs : measurable_set s) :
  is_stopping_time ð’¢ (hitting_time g s) :=
begin
  intro i,
  by_cases hi_top : i = âŠ¤,
  { rw hi_top, simp, },
  simp_rw hitting_time_le_iff hi_top,
  have : {x : Î± | âˆƒ j â‰¤ i, g j x âˆˆ s} = â‹ƒ j â‰¤ i, {x | g j x âˆˆ s},
  { ext1 x, simp only [set.mem_set_of_eq, set.mem_Union], },
  rw this,
  refine measurable_set.bUnion (set.countable_encodable _) (Î» n hn, ð’¢.mono (hn) _ _),
  exact (hg n).measurable hs,
end

end nat

end probability_theory
